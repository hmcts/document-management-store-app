plugins {
    id 'application'
    id 'checkstyle'
    id 'pmd'
    id 'io.spring.dependency-management' version '1.0.4.RELEASE'
    id 'org.springframework.boot' version '1.5.10.RELEASE'
    id 'org.owasp.dependencycheck' version '3.1.0'
    id 'com.github.ben-manes.versions' version '0.17.0'
    id 'org.sonarqube' version '2.5'
    id 'jacoco'
    id 'idea'
    id 'eclipse'
}

group = 'uk.gov.hmcts.reform.dm'
version = '1.1.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

//    XLINT
compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

tasks.withType(Test) {
    maxParallelForks = 1
}

// Xlint type thingy????
checkstyle {
    showViolations = true
    ignoreFailures = true
    maxWarnings = 0
    toolVersion = '8.7'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "5.8.1"
    ignoreFailures = true
    sourceSets = [sourceSets.main, sourceSets.test]
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

//    Report Check Style added by Alex
tasks.withType(Checkstyle) {
    reports {
        xml.enabled false
        html.enabled true
    }
}

//    OWASP (./gradlew -DdependencyCheck.failBuild=true dependencyCheck;)
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

repositories {
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    jcenter()
    mavenLocal()
}

apply from: 'liquibase.gradle'

def springBootVersion = plugins.getPlugin('org.springframework.boot').class.package.implementationVersion

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${springBootVersion}"
    }
}

def versions = [
    ehcache            : '3.3.1',
    jacocoMavenPlugin  : '0.7.9',
    jackson            : dependencyManagement.importedProperties['jackson.version'],
    javaxWsRsApi       : '2.0.1',
    jsonassert         : '1.4.0',
    jsonUnitFluent     : '1.21.0',
    JUnitParams        : '1.1.0',
    lombok             : '1.16.18',
    liquibase          : '3.5.3',
    mavenFailsafePlugin: '2.19.1',
    owasp              : '3.0.2',
    postgresql         : '42.2.1',
    powermock          : '1.6.5',
    sonar              : '3.2',
    springBoot         : springBootVersion,
    springCloud        : 'Camden.SR5',
    springfoxSwagger   : '2.7.0',
    xmlMavenPlugin     : '1.0',
]

dependencies {
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.8'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.17'
    compile group: 'org.apache.tomcat', name: 'tomcat-annotations-api', version: '8.5.28'

    compile group: 'commons-io', name: 'commons-io', version: '2.5'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.8.11'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.8.11'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.8.11.1'

    compile group: 'net.logstash.logback', name: 'logstash-logback-encoder', version: '4.11'

    compile group: 'org.ehcache', name: 'ehcache', version: versions.ehcache

    compile group: 'org.springframework', name: 'spring-context-support', version: '4.3.8.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-batch', version: versions.springBoot
    compile group: 'org.springframework.hateoas', name: 'spring-hateoas', version: '0.24.0.RELEASE'

    compile group: 'org.liquibase', name: 'liquibase-core', version: versions.liquibaseVersion

    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger

    compile group: 'org.postgresql', name: 'postgresql', version: versions.postgresqlVersion

    compile group: 'org.projectlombok', name: 'lombok', version: versions.lombokVersion

    compile group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.1'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-spring', version: '1.2.1'
    compile group: 'uk.gov.hmcts.reform', name: 'java-logging-httpcomponents', version: '1.2.1'
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: '1.1.0'

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix', version: '1.4.0.RELEASE'
    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-hystrix-dashboard', version: '1.4.0.RELEASE'

    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.2.0'

    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude(module: 'commons-logging')
    }

    testCompile(group: 'com.github.tomakehurst', name: 'wiremock', version: '2.1.7') {
        exclude(module: 'jetty')
        exclude(module: 'httpclient')
        exclude(module: 'json-path')
        exclude(module: 'jopt-simple')
        exclude(module: 'jetty-server')
        exclude(module: 'jetty-servlet')
        exclude(module: 'jetty-servlets')
        exclude(module: 'jetty-webapp')
    }
    testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: '1.21.0'
    testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: '1.21.0'
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '4.2.3.RELEASE'

    testCompile group: 'com.h2database', name: 'h2', version: '1.0.60'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: versions.powermock
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: versions.powermock

    testCompile group: 'org.eclipse.jetty', name: 'jetty-server', version: '9.4.7.v20170914'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '9.4.7.v20170914'
    testCompile group: 'org.eclipse.jetty', name: 'jetty-servlets', version: '9.4.7.v20170914'
    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: '1.1.0'


}

def coverageExclusionList = [
    '**uk/gov/hmcts/dm/domain/',
    '**uk/gov/hmcts/dm/DmApp.java',
    '**uk/gov/hmcts/dm/hateos/*',
    '**uk/gov/hmcts/dm/exception/*',
    '**uk/gov/hmcts/dm/domain/*',
    '**uk/gov/hmcts/dm/commandobject/*',
    '**uk/gov/hmcts/dm/hibernate/*',
    '**uk/gov/hmcts/dm/config/**/*',
    '**uk/gov/hmcts/dm/errorhandler/*',
    '**uk/gov/hmcts/dm/repository/RepositoryFinder.java',
    '**uk/gov/hmcts/dm/service/batch/CustomItemWriter.java',
    '**uk/gov/hmcts/dm/actuate/health/IdamHealth.java',
    '**uk/gov/hmcts/dm/actuate/health/IdamServiceHealth.java'
]

//(jacocoTestReport | jacocoTestCoverageVerification)
jacoco {
    toolVersion = '0.7.9' // jacocoMavenPluginVersion
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        csv.enabled = true
    }

    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: coverageExclusionList)
        })
    }
}

//(./gradlew sonarqube)
sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", coverageExclusionList.join(", ")
    }
}

//(./gradlew clean jar) build/libs/em-api-*-all.jar
//(./gradlew installDist bootRepackage) build/install/document-management-store-app/bin/document-management-store-app
jar {
    baseName 'document-management-store-app'
    destinationDir = file("$rootDir/build/libs")
    manifest {
        attributes 'Implementation-Title': project.name, 'Implementation-Version': project.version
    }
}

bootRepackage {
    classifier 'all'
}

//This is used in get the version for fakeversion and buildInfo  (./gradlew -q printVersion)
task printVersion {
    description 'Prints the version of this project'
    doLast {
        print project.version
    }
}

// Run the app (./gradlew run -Ddebug=true)
run {
    def debug = System.getProperty("debug")
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5005']
    }
}
