
configurations {
    liquibase
    liquibaseRuntime
}

def liquibaseversions = [
    liquibase          : '4.8.0',
    hibernate          : '5.6.5.Final',
    postgresql         : '42.2.20'
]

dependencies {
    implementation group: 'com.github.hmcts.liquibase', name: 'liquibase-core', version: '3.8.2'
    liquibase group: 'org.liquibase.ext', name: 'liquibase-hibernate5', version: liquibaseversions.liquibase
    liquibase group: 'org.hibernate', name: 'hibernate-envers', version: liquibaseversions.hibernate
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-core', version: liquibaseversions.liquibase
    liquibaseRuntime group: 'org.liquibase.ext', name: 'liquibase-hibernate5', version: liquibaseversions.liquibase
    liquibaseRuntime sourceSets.main.compileClasspath
    liquibaseRuntime sourceSets.main.output
    liquibaseRuntime group: 'org.liquibase', name: 'liquibase-groovy-dsl', version: '3.0.2'
    liquibaseRuntime group: 'org.postgresql', name: 'postgresql', version: liquibaseversions.postgresql
    liquibaseRuntime group: 'com.h2database', name: 'h2'
}

//loading properties file.
Properties liquibaseProps = new Properties()
liquibaseProps.load(new FileInputStream("$projectDir/src/main/resources/liquibase.properties"))

task liquibaseDiffChangelog(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"
    systemProperty "liquibase.useDbLock", true
    def urlString = project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
    def user = project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
    def password = project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')

    dependsOn 'clean'
    dependsOn 'compileJava'

    args "--changeLogFile=" + "$projectDir/src/main/resources/db/changelog/db.changelog-diff-" + buildTimestamp() + ".xml"
    args "--referenceUrl=hibernate:spring:uk.gov.hmcts.dm.domain?dialect=org.hibernate.dialect.PostgreSQLDialect"
    args "--username=$user"
    args "--password=$password"
    args "--url=$urlString"
    args "--driver=" + liquibaseProps.getProperty('driver')
    args "diffChangeLog"
}

task migratePostgresDatabase(type: JavaExec) {
    group = "liquibase"

    classpath sourceSets.main.runtimeClasspath
    classpath configurations.liquibase
    main = "liquibase.integration.commandline.Main"
    systemProperty "liquibase.useDbLock", true
    def urlString = project.hasProperty("dburl") ? "jdbc:postgresql://$dburl" : liquibaseProps.getProperty('url')
    def user = project.hasProperty("flyway.user") ? "${rootProject.properties['flyway.user']}" : liquibaseProps.getProperty('username')
    def password = project.hasProperty("flyway.password") ? "${rootProject.properties['flyway.password']}" : liquibaseProps.getProperty('password')

    args "--changeLogFile=./src/main/resources/db/changelog/db.changelog-master.xml"
    args "--username=$user"
    args "--password=$password"
    args "--url=$urlString"
    args "--driver=" + liquibaseProps.getProperty('driver')
    args "update"
}

def buildTimestamp() {
    def date = new Date()
    def formattedDate = date.format('yyyyMMdd-HHmm')
    return formattedDate
}
