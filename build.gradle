buildscript {
    dependencies {
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.3.13")
        classpath("net.serenity-bdd:serenity-single-page-report:2.3.13")
    }
}

plugins {
    id 'application'
    id 'groovy'
    id 'uk.gov.hmcts.java' version '0.12.9'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'org.springframework.boot' version '2.3.1.RELEASE'
    id 'com.github.ben-manes.versions' version '0.38.0'
    id 'org.sonarqube' version '3.2.0'
    id 'jacoco'
    id 'checkstyle'
    id 'pmd'
    id 'io.franzbecker.gradle-lombok' version '4.0.0'
    id "info.solidsoft.pitest" version '1.5.2'
    id 'au.com.dius.pact' version '4.1.8'

}

apply plugin: 'java'
apply plugin: 'idea'
apply from: 'liquibase.gradle'
apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.dm'
version = '2.0.0'
mainClassName = 'uk.gov.hmcts.dm.DmApp'
sourceCompatibility = 11
targetCompatibility = 11

sourceSets {
    functionalTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/functionalTest/java')
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/functionalTest/groovy')
        }
        resources.srcDir file('src/functionalTest/resources')
    }

    smokeTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/smokeTest/java')
        }
        groovy {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/smokeTest/groovy')
        }
        resources.srcDir file('src/smokeTest/resources')
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

configurations {
    functionalTestCompile.extendsFrom(testCompile)
    functionalTestRuntime.extendsFrom(testRuntime)
    smokeTestCompile.extendsFrom(testCompile)
    smokeTestRuntime.extendsFrom(testRuntime)
    contractTestCompile.extendsFrom(testCompile)
    contractTestRuntime.extendsFrom(testRuntime)
    functionalTestCompile.extendsFrom(testCompile)
    functionalTestRuntime.extendsFrom(testRuntime)

}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

//tasks.withType(Test) {
//    maxParallelForks = 3
//}

repositories {
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    jcenter()
    mavenLocal()
}

def versions = [
    ehcache            : '3.7.1',
    jacocoMavenPlugin  : '0.8.2',
    jsonUnit           : '1.31.1',
    JUnitParams        : '1.1.1',
    lombok             : '1.18.10',
    liquibase          : '3.8.1',
    postgresql         : '42.2.14',
    jackson            : '2.11.3',
    mockitoJupiter     : '3.0.0',
    junit              : '5.6.3',
    junitPlatform      : '1.5.1',
    powermock          : '1.7.4',
    serenity           : '2.3.13',
    serenityreporter   : '2.3.13',
    serenityRestAssured: '2.3.4',
    springBoot         :  springBoot.class.package.implementationVersion,
    logging            : '5.1.5',
    pitest             : '1.5.2',
    gradlePitest       : '1.3.0',
    sonarPitest        : '0.5',
    pact_version       : '4.1.7',
    emTestHelper       : '1.18.1'
]

configurations.all {
    resolutionStrategy {
        eachDependency { DependencyResolveDetails details ->
            if (details.requested.group in ['org.apache.tomcat.embed']) {
                details.useVersion '9.0.43'
            }
            if (details.requested.name in ['guava']) {
                details.useVersion '30.1-jre'
            }
        }
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-starter-parent:${versions.springBoot}"
    }

    dependencies {
        // align with jupiter
        dependencySet(group: 'org.mockito', version: versions.mockitoJupiter) {
            entry 'mockito-core'
        }

        //CVE-2021-21290
        dependencySet(group: 'io.netty', version: '4.1.63.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-resolver'
            entry 'netty-transport'
            entry 'netty-codec-http'
            entry 'netty-codec-http2'
            entry 'netty-transport-native-epoll'
            entry 'netty-transport-native-kqueue'
            entry 'netty-transport-native-unix-common'
            entry 'netty-codec-socks'
            entry 'netty-handler-proxy'
        }

        //CVE-2021-22112
        dependencySet(group: 'org.springframework.security', version: '5.4.4') {
            entry 'spring-security-config'
            entry 'spring-security-core'
            entry 'spring-security-crypto'
            entry 'spring-security-web'
        }

        //CVE-2020-5421
        dependencySet(group: 'org.springframework', version: '5.2.9.RELEASE') {
            entry 'spring-aop'
            entry 'spring-aspects'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-jdbc'
            entry 'spring-orm'
            entry 'spring-tx'
            entry 'spring-web'
            entry 'spring-webmvc'
        }
    }
}

dependencies {
    compile group: 'com.azure', name: 'azure-storage-blob', version: '12.6.0'
    compile group: 'org.apache.pdfbox', name: 'pdfbox', version: '2.0.23'
    compile group: 'org.apache.tika', name: 'tika-core', version: '1.26'

    compile group: 'org.apache.tomcat', name: 'tomcat-annotations-api', version: '10.0.5'
    compile group: 'org.apache.tomcat.embed', name: 'tomcat-embed-core', version: '10.0.5'

    compile group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'

    compile group: 'commons-io', name: 'commons-io', version: '2.9.0'
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: versions.jackson
    compile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    compile group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: versions.jackson

    compile group: 'org.ehcache', name: 'ehcache', version: versions.ehcache

    compile group: 'org.springframework', name: 'spring-context-support', version: '5.3.5'
    compile group: 'org.springframework.boot', name: 'spring-boot-devtools', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-actuator', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-aop', version: versions.springBoot
    compile (group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: versions.springBoot) {
        exclude group: "org.hibernate", module: "hibernate-core"
    }
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-rest', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-batch', version: versions.springBoot
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-validation', version: versions.springBoot

    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'

    compile group: 'org.hibernate', name: 'hibernate-core', version: '5.4.30.Final'

    compile group: 'uk.gov.hmcts.reform', name: 'liquibase-core', version: '3.8.1'

    compile group: 'io.swagger', name: 'swagger-core', version: '1.6.1'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.6.1'

    compile group: 'org.postgresql', name: 'postgresql', version: versions.postgresql

    compile group: "net.javacrumbs.shedlock", name: "shedlock-spring", version:"4.24.0"
    compile group: "net.javacrumbs.shedlock", name: "shedlock-provider-jdbc-template", version:"4.24.0"

    compileOnly group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    compile (group: 'uk.gov.hmcts.reform.auth', name: 'auth-checker-lib', version: '2.1.4') {
        exclude group: 'uk.gov.hmcts.reform', module: 'java-logging-spring'
    }
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.logging
    compile ("uk.gov.hmcts.reform:properties-volume-spring-boot-starter:0.1.0"){
        exclude group: 'org.springframework.cloud' , module : 'spring-cloud-context'
        exclude group: 'org.springframework.boot' , module: 'spring-boot-starter-web'
    }

    compile group: 'org.springframework.cloud', name: 'spring-cloud-starter-netflix-hystrix', version: '2.1.5.RELEASE'
    compile (group: 'org.springframework.cloud',
        name: 'spring-cloud-starter-netflix-hystrix-dashboard', version: '2.1.5.RELEASE') {
        exclude group: 'org.webjars', module: 'jquery'
    }

    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.13'
    compile group: 'org.apache.httpcomponents', name: 'fluent-hc', version: '4.5.13'

    testCompile group: 'com.jayway.jsonpath', name: 'json-path', version: '2.5.0'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version: versions.springBoot) {
        exclude(module: 'commons-logging')
    }

    testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit', version: versions.jsonUnit
    testCompile group: 'net.javacrumbs.json-unit', name: 'json-unit-fluent', version: versions.jsonUnit
    testCompile group: 'org.springframework.security', name: 'spring-security-test', version: '5.4.4'

    testCompile group: 'com.h2database', name: 'h2', version: '1.4.200'
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit
    testCompile group: 'org.junit.jupiter', name: 'junit-jupiter-params', version: versions.junit
    
    testCompile group: 'org.mockito', name: 'mockito-junit-jupiter', version: versions.mockitoJupiter

    testCompile group: 'pl.pragmatists', name: 'JUnitParams', version: versions.JUnitParams
    testCompile group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '3.0.0'
    testCompile (group: 'io.rest-assured', name :'rest-assured', version:'3.3.0') {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    testCompile group: 'com.warrenstrange', name: 'googleauth', version: '1.5.0'
    testCompile group: 'org.codehaus.groovy', name :'groovy-all', version:'2.4.17'
    testCompile group: 'com.github.stephenc.jcip', name :'jcip-annotations', version:'1.0-1'

    testCompile (group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenityRestAssured) {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    testCompile (group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity) {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    testCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    testCompile group: 'net.serenity-bdd', name: 'serenity-single-page-report', version: versions.serenityreporter

    functionalTestCompile group: 'uk.gov.hmcts.reform', name: 'em-test-helper', version: versions.emTestHelper

    //pact
    contractTestCompile group: 'au.com.dius.pact.provider', name: 'junit5', version: versions.pact_version
    contractTestCompile group: 'au.com.dius.pact.provider', name: 'spring', version: versions.pact_version
    contractTestCompile group: 'au.com.dius.pact.provider', name: 'junit5spring', version: versions.pact_version
    contractTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    contractTestCompile("org.junit.jupiter:junit-jupiter-api:5.7.1")
    contractTestRuntime("org.junit.jupiter:junit-jupiter-engine:5.7.1")
    contractTestImplementation('org.junit.jupiter:junit-jupiter-api:5.7.1')
    contractTestCompile sourceSets.main.runtimeClasspath
    contractTestCompile sourceSets.test.runtimeClasspath
}

dependencyCheck {
    suppressionFile = 'config/owasp/dependency-check-suppressions.xml'
}

checkstyle {
    maxWarnings = 0
    toolVersion = '8.34'
    // need to set configDir to rootDir otherwise submodule will use submodule/config/checkstyle
    configDir = new File(rootDir, 'config/checkstyle')
}

pmd {
    toolVersion = "6.29.0"
    ignoreFailures = true
    reportsDir = file("$project.buildDir/reports/pmd")
    ruleSetFiles = files("config/pmd/ruleset.xml")
}

def coverageExclusionList = [
    '**uk/gov/hmcts/dm/domain/',
    '**uk/gov/hmcts/dm/DmApp.java',
    '**uk/gov/hmcts/dm/hateos/*',
    '**uk/gov/hmcts/dm/exception/*',
    '**uk/gov/hmcts/dm/domain/*',
    '**uk/gov/hmcts/dm/commandobject/*',
    '**uk/gov/hmcts/dm/hibernate/*',
    '**uk/gov/hmcts/dm/config/**/*',
    '**uk/gov/hmcts/dm/errorhandler/*',
    '**uk/gov/hmcts/dm/repository/RepositoryFinder.java',
    '**uk/gov/hmcts/dm/service/batch/CustomItemWriter.java',
    '**uk/gov/hmcts/dm/appinsights/*',
    '**uk/gov/hmcts/dm/actuate/health/IdamHealth.java',
    '**uk/gov/hmcts/dm/actuate/health/IdamServiceHealth.java',
    '**uk/gov/hmcts/dm/service/FileSizeVerifier.java',
    '**uk/gov/hmcts/dm/service/BlobStorageReadService.java',
    '**uk/gov/hmcts/dm/controller/WelcomeController.java'
]

//(jacocoTestReport | jacocoTestCoverageVerification)
jacoco {
    toolVersion = '0.8.2' // jacocoMavenPluginVersion
    reportsDir = file("$buildDir/reports/jacoco")
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        csv.enabled = false

        xml.destination file("${project.buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }

}

pitest {
    targetClasses = ['uk.gov.hmcts.dm.*']
    excludedClasses = [
        'uk.gov.hmcts.dm.appinsights.*' ,
        'uk.gov.hmcts.dm.authchecker.*' ,
        'uk.gov.hmcts.dm.domain.*',
        'uk.gov.hmcts.dm.DmApp.java',
        'uk.gov.hmcts.dm.service.util.*'
    ]

    enableDefaultIncrementalAnalysis = true
    historyInputLocation = 'build/reports/pitest/fastermutationtesting'
    historyOutputLocation ='build/reports/pitest/fastermutationtestingoutput'
    threads = 15
    testSourceSets = [sourceSets.test]
    mainSourceSets = [sourceSets.main]
    fileExtensionsToFilter.addAll('xml','json')
    outputFormats = ['XML', 'HTML','CSV']
    mutationThreshold = 5
    coverageThreshold = 0
    maxMutationsPerClass = 15
    jvmArgs = ['-Xms1G','-Xmx3G']
    timestampedReports = false
    failWhenNoMutations = false
    detectInlinedCode = true

}

project.tasks['sonarqube'].group = "Verification"

project.tasks['sonarqube'].dependsOn test, jacocoTestReport

sonarqube {
    properties {
        property "sonar.projectName", "${rootProject.name}"
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.exclusions", coverageExclusionList.join(", ")
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

bootJar {
    getArchiveFileName().set(provider {
        'dm-store.jar'
    })

    manifest {
        attributes('Implementation-Version': project.version.toString())
    }
}

//This is used in get the version for fakeversion and buildInfo  (./gradlew -q printVersion)
task printVersion {
    description 'Prints the version of this project'
    doLast {
        print project.version
    }
}

test {
    useJUnitPlatform()
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    doFirst(){
        printf("Available processors:::::::::: " + Runtime.runtime.availableProcessors().intdiv(2).toString())
    }

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 10

    group = 'Delivery pipeline'
    description = 'Executes functional tests'

    setTestClassesDirs(sourceSets.functionalTest.output.classesDirs)
    setClasspath(sourceSets.functionalTest.runtimeClasspath)

    failFast = true

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")

    finalizedBy aggregate
}

serenity {
    reports = ["single-page-html"]
}

task smoke(type: Test) {
    group = 'Delivery pipeline'
    description = 'Executes non-destructive smoke tests'
    setTestClassesDirs(sourceSets.smokeTest.output.classesDirs)
    setClasspath(sourceSets.smokeTest.runtimeClasspath)
    include "uk/gov/hmcts/dm/smoke/**"

    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}
project.ext {
    pactVersion = getCheckedOutGitCommitHash()
}
def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

task runProviderPactVerification(type: Test) {
    logger.lifecycle("Runnning contract Tests")
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    systemProperty 'pact.verifier.publishResults', System.getProperty('pact.verifier.publishResults')
}

runProviderPactVerification.finalizedBy pactVerify
